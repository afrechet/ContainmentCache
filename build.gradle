apply plugin: 'java'
apply plugin: "maven"
apply plugin: 'application'

repositories {
    //EARG maven repo.
    maven {
        url 'https://bitbucket.org/EARG-builder/maven-repo/raw/master/releases/'
    }
    maven {
        url 'https://bitbucket.org/EARG-builder/maven-repo/raw/master/snapshots/'
    }
    mavenLocal()
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

//Project properties.
version = '0.9.2'
group = 'ca.ubc.cs.beta'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	compile 'com.google.guava:guava:18.0'
	compile 'org.apache.commons:commons-collections4:4.0'
	compile 'org.apache.commons:commons-lang3:3.0'
	compile 'org.apache.commons:commons-math3:3.4.1'
	
    compile 'ch.qos.logback:logback-classic:1.0.13'
	compile 'org.slf4j:slf4j-api:1.7.7'
	compile 'org.projectlombok:lombok:1.14.8'
	compile 'net.jcip:jcip-annotations:1.0'

    compile 'com.beust:jcommander:1.35-patched'
    compile 'ca.ubc.cs.beta:aeatk:2.08.01-development-de3ee6617d5c'

    testCompile 'junit:junit:4.11'
}

test {
     beforeTest { descriptor ->
         logger.lifecycle(System.lineSeparator + "Running test: " + descriptor)
     }
     testLogging {
         events "passed", "skipped", "failed", "standardError"
     }
}


mainClassName = 'containmentcache.KevinTest'
applicationDefaultJvmArgs = ['-Xms16g', '-Xmx 24g']

//Publishing to EARG maven repo.

apply plugin: 'maven-publish'

public String getEARGMavenRepo()
{
   Properties properties = new Properties()
   def propertyFilename = 'local.properties'
   def propertyFile = project.rootProject.file(propertyFilename)
   if(!propertyFile.exists()) {
      println 'Necessary file '+propertyFilename+' was not present, creating an empty version of it!'
      propertyFile.createNewFile()
   }
   properties.load(propertyFile.newDataInputStream())
   def eargMavenRepo = properties.getProperty('earg.mavenrepo.dir')
   return eargMavenRepo
}

task sourceJar(type: Jar) {
   from sourceSets.main.allJava
}

publishing {

   repositories {
       //Local EARG maven repo.   
       maven {
          url getEARGMavenRepo()+'/snapshots/'
       }
   }
   
   publications {
   
      maven(MavenPublication) {
         groupId 'ca.ubc.cs.beta'
         artifactId 'ContainmentCache'
         version version
         
         from components.java
         
         artifact sourceJar {
            classifier "sources"
         }
      }
   
   }
}